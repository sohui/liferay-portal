import org.gradle.api.artifacts.ResolveException

apply plugin: "com.liferay.target.platform"

task runGradleTest

dependencies {
	targetPlatformBoms group: "com.liferay.portal", name: "release.portal.bom", version: "7.1.3"
}

runGradleTest {
	doLast {
		Project subproject1 = project.childProjects["subproject1"]

		Exception expectedException = null

		try {
			subproject1.configurations.compile.resolvedConfiguration.resolvedArtifacts
		}
		catch (ResolveException re) {
			expectedException = re.cause
		}

		assert expectedException.message.contains("Could not find com.liferay.portal:com.liferay.portal.kernel:")

		Project subproject2 = project.childProjects["subproject2"]

		Configuration configuration = subproject2.configurations.compile

		Map<String, String> managedVersionsForConfiguration = subproject2.dependencyManagement.getManagedVersionsForConfiguration(configuration)

		assert managedVersionsForConfiguration.size() == 877

		expectedException = null

		try {
			configuration.resolvedConfiguration.resolvedArtifacts
		}
		catch (ResolveException re) {
			expectedException = re.cause
		}

		assert expectedException.message.contains("com.liferay.portal:com.liferay.portal.kernel:3.63.1")
	}
}

targetPlatform {
	onlyIf {
		it.name != "subproject1"
	}
}