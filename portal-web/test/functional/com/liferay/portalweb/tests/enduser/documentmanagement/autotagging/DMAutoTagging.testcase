@component-name = "portal-document-management"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Auto Tagging";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		AutoTagging.enableAutoTagging(enableAutoTaggingFor = "System Settings");

		AutoTagging.enableTensorFlow();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			DMDocument.tearDownCP();

			DMFolder.tearDownCP();

			User.logoutPG();
		}
	}

	@description = "This test asserts that auto-tags added to an image uploaded on a staged site gets published to Live."
	@priority = "4"
	test AssertAutoTagAfterPublishToLive {
		property test.name.skip.portal.instance = "DMAutoTagging#AssertAutoTagAfterPublishToLive";

		JSONGroup.addGroup(groupName = "Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Documents and Media Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Site Name",
			layoutName = "Documents and Media Page",
			widgetName = "Documents and Media");

		Navigator.gotoSitePage(
			pageName = "Documents and Media Page",
			siteName = "Site Name");

		Portlet.addPG(portletName = "Documents and Media");

		DMDocument.enableActionsMenuOnPortlet();

		ProductMenu.gotoPortlet(
			category = "Publishing",
			panel = "Site Administration",
			portlet = "Staging");

		Staging.activateStagingCP(siteName = "Site Name");

		Navigator.gotoStagedSitePage(
			pageName = "Documents and Media Page",
			siteName = "Site Name");

		DMDocument.addPG(
			dmDocumentDescription = "Document Description",
			dmDocumentFile = "Document_4.jpg",
			dmDocumentTitle = "Document Title");

		Navigator.gotoStagedSitePage(
			pageName = "Documents and Media Page",
			siteName = "Site Name");

		DMDocument.viewPG(
			dmDocumentDescription = "Document Description",
			dmDocumentTitle = "Document Title");

		AutoTagging.assertAutoTagPresent(tagName = "carton");

		Staging.gotoPublishToLive();

		Staging.publishToLive();

		ProductMenu.gotoLiveSite();

		Navigator.gotoSitePage(
			pageName = "Documents and Media Page",
			siteName = "Site Name");

		DMDocument.viewPG(
			dmDocumentDescription = "Document Description",
			dmDocumentTitle = "Document Title");

		AutoTagging.assertAutoTagPresent(tagName = "carton");
	}

	@description = "This test asserts that no auto-tag is generated when an image is uploaded to a site that has auto-tagging disabled despite the auto-tagging functionality enabled at portal level."
	@priority = "4"
	test ConfigureAutoTaggingPerSite {
		property test.name.skip.portal.instance = "DMAutoTagging#ConfigureAutoTaggingPerSite";

		JSONGroup.addGroup(groupName = "Site AutoTag Enabled");

		JSONLayout.addPublicLayout(
			groupName = "Site AutoTag Enabled",
			layoutName = "AutoTag Enabled Site Page");

		JSONGroup.addGroup(groupName = "Site AutoTag Disabled");

		JSONLayout.addPublicLayout(
			groupName = "Site AutoTag Disabled",
			layoutName = "AutoTag Disabled Site Page");

		ProductMenu.gotoSite(site = "Site AutoTag Disabled");

		AutoTagging.disableAutoTagging(disableAutoTaggingFor = "Settings");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "site-autotag-enabled");

		DMDocument.addCP(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_4.jpg",
			dmDocumentTitle = "DM Document JPG");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "site-autotag-enabled");

		DMNavigator.gotoDocumentCP(dmDocumentTitle = "DM Document JPG");

		DMDocument.viewCP(dmDocumentTitle = "DM Document JPG");

		AutoTagging.assertAutoTagPresent(tagName = "carton");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "site-autotag-disabled");

		DMDocument.addCP(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_4.jpg",
			dmDocumentTitle = "DM Document JPG");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "site-autotag-disabled");

		DMNavigator.gotoDocumentCP(dmDocumentTitle = "DM Document JPG");

		DMDocument.viewCP(dmDocumentTitle = "DM Document JPG");

		AutoTagging.assertNoAutoTag();
	}

	@description = "This test asserts that no auto-tags are generated for manually tagged images."
	@priority = "4"
	test NoAutoTagForManuallyTaggedImage {
		property test.name.skip.portal.instance = "DMAutoTagging#NoAutoTagForManuallyTaggedImage";

		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Documents and Media Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Guest",
			layoutName = "Documents and Media Page",
			widgetName = "Documents and Media");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.enableActionsMenuOnPortlet();

		Navigator.gotoPage(pageName = "Documents and Media Page");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		DMDocument.addPG(
			dmDocumentDescription = "Document Description",
			dmDocumentFile = "Document_4.jpg",
			dmDocumentTitle = "Document Title",
			tagNameList = "ManualTag,NotAutoTagged");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.viewPG(
			dmDocumentDescription = "Document Description",
			dmDocumentTitle = "Document Title",
			tagNameList = "manualtag,notautotagged");

		AutoTagging.assertNoAutoTag(tagName = "carton");
	}

	@description = "This test asserts that auto-tags can be deleted."
	@priority = "4"
	test RemoveAutoTag {
		property test.name.skip.portal.instance = "DMAutoTagging#RemoveAutoTag";

		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Documents and Media Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Guest",
			layoutName = "Documents and Media Page",
			widgetName = "Documents and Media");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.enableActionsMenuOnPortlet();

		Navigator.gotoPage(pageName = "Documents and Media Page");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		DMDocument.addPG(
			dmDocumentDescription = "Auto Tagged Image Description",
			dmDocumentFile = "Document_4.jpg",
			dmDocumentTitle = "Auto Tagged Image");

		DMDocument.viewPG(
			dmDocumentDescription = "Auto Tagged Image Description",
			dmDocumentTitle = "Auto Tagged Image",
			tagNameList = "carton");

		DMDocument.removeTag(removeTag = "carton");

		Button.clickPublish();

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.viewPG(
			dmDocumentDescription = "Auto Tagged Image Description",
			dmDocumentTitle = "Auto Tagged Image");

		AutoTagging.assertNoAutoTag();
	}

	@description = "This test asserts that an image is auto-tagged during upload when Auto-Tagging is enabled."
	@priority = "5"
	test Smoke {
		property portal.acceptance = "true";
		property test.name.skip.portal.instance = "DMAutoTagging#Smoke";

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMDocument.addCP(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_4.jpg",
			dmDocumentTitle = "DM Document JPG");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMNavigator.gotoDocumentCP(dmDocumentTitle = "DM Document JPG");

		DMDocument.viewCP(dmDocumentTitle = "DM Document JPG");

		AutoTagging.assertAutoTagPresent(tagName = "carton");
	}

}